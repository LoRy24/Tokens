import java.text.SimpleDateFormat

apply plugin: 'java'
group = 'net.thirdshift'
 
println """
==========================================================================
You are building the Tokens plugin https://github.com/TheCoolestPaul/Tokens
  Version:    ${project.version} 
  Build time: ${getCurrentTimestamp()}

Output files are located in [project]/build/libs/Tokens-${project.version}.jar
==========================================================================
"""


sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        name = "SpigotMC"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = 'placeholderapi-repo'
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        name = "Jitpack-io"
        url = "https://jitpack.io"
    }
    maven {
        name = "codeMC repo"
        url = "https://repo.codemc.io/repository/maven-public/"
    }
    maven {
        name = "factions repo"
        url = "https://ci.ender.zone/plugin/repository/everything/"
    }
}

dependencies {
    implementation "org.spigotmc:spigot-api:$spigotVersion"
    implementation ("com.github.MilkBowl:VaultAPI:$vaultVersion") {
        exclude group: "org.bukkit"
    }
    implementation ("me.clip:placeholderapi:$placeholderVersion") {
        exclude group: "org.bstats"
    }
    implementation ("com.github.mcMMO-Dev:mcMMO:$mcmmoVersion") {
        exclude group: "com.sk89q.worldguard"
    }
    implementation "com.SirBlobman.combatlogx:CombatLogX-API:$combatlogxVersion"
    implementation ("com.massivecraft:Factions:$factionsVersion") {
        exclude group: "org.spongepowered"
        exclude group: "com.darkblade12"
        exclude group: "com.mojang"
        exclude group: "org.kitteh"
        exclude group: "io.papermc"
    }
    implementation ("com.github.brcdev-minecraft:shopgui-api:$shopguiplusVersion") {
        exclude group: "org.spigotmc"
    }
    implementation ("com.github.Picono435:PicoJobs:$picoJobsVersion"){

    }

    implementation "junit:junit:4.13"
}

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation"
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from(sourceSets.main.resources.srcDirs) {
        expand 'version': project.version
        include 'plugin.yml'
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

static def getCurrentTimestamp() {
  SimpleDateFormat df = new SimpleDateFormat ("HH:mm:ss MM-dd-yyyy")
  return df.format (new Date())
}
