import java.text.SimpleDateFormat

apply plugin: 'java'

group = 'net.thirdshift'

 
println """
==========================================================================
You are building the Tokens plugin https://github.com/TheCoolestPaul/Tokens
  Version:    ${project.version} 
  Build time: ${getCurrentTimestamp()}

Output files are located in [project]/build/libs/Tokens-${project.version}.jar
==========================================================================
"""


sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
    maven {
        name = 'placeholderapi-repo'
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
    maven {
        name = "Vault-API"
        url "http://nexus.hc.to/content/repositories/pub_releases"
    }
    maven {
        name = "Jitpack-io"
        url = "https://jitpack.io"
    }
    maven {
        name = "codeMC repo"
        url = "https://repo.codemc.io/repository/maven-public/"
    }
    maven {
        name = "maven NMS"
        url = "https://repo.codemc.io/repository/nms/"
    }
    maven {
        name = "worldguard because mcmmo is a needy dependency"
        url = "https://maven.enginehub.org/repo/"
    }

    // Added for Factions
    maven {
        name = "factions repo"
        url = "https://repo.dustplanet.de/artifactory/ext-release-local/"
    }

    // Added for Vault dependencies
    maven { url = 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots/' }
}

dependencies {
    compileOnly "org.spigotmc:spigot:$spigotVersion"
    compileOnly "net.milkbowl.vault:VaultAPI:$vaultVersion"
    compileOnly "me.clip:placeholderapi:$placeholderVersion"
    compileOnly "com.gmail.nossr50.mcMMO:mcMMO:$mcmmoVersion"
    compileOnly "com.SirBlobman.combatlogx:CombatLogX-API:$combatlogxVersion"

    compileOnly "com.massivecraft:Factions:$factionsVersion"

    compileOnly "com.github.brcdev-minecraft:shopgui-api:$shopguiplusVersion"
    
    testCompile 'junit:junit:4.12'
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        expand 'version': project.version
        include 'plugin.yml'
    }
    from(sourceSets.main.resources.srcDirs) {
        exclude 'plugin.yml'
    }
}

static def getCurrentTimestamp() {
  SimpleDateFormat df = new SimpleDateFormat ("HH:mm:ss MM-dd-yyyy")
  return df.format (new Date())
}
